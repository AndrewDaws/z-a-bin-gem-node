# Copyright (c) 2019 Sebastian Gniazdowski
# License MIT


# FUNCTION: .za-bgn-bin-or-src-function-body
# Returns the body of the function wrapping a binary
local name="$2" bin="$3" dir="$4"
integer run_type="$1" set_gem_home="$5" \
    set_node_path="$6" set_cwd="$7"

local nl=$'\n' tab=$'    '

REPLY="function ${(q)name} {
    local bindir=${(qqq)bin:h}
    ${${(M)set_gem_home:#1}:+local -x GEM_HOME=${(qqq)dir}}
    ${${(M)set_node_path:#1}:+local -x NODE_PATH=${(qqq)dir}/node_modules}
    ${${(M)set_cwd:#1}:+"local oldpwd=${(qqq)PWD}
    () { setopt localoptions noautopushd; builtin cd -q ${(qqq)dir}; }"}
"

if (( run_type == 0 )); then
    REPLY+="$tab\"\$bindir\"/${(qqq)bin:t} \"\$@\"$nl"
elif (( run_type == 1 )); then
    REPLY+="$tab() { source \"\$bindir\"/${(qqq)bin:t}; } \"\$@\"$nl"
elif (( run_type == 2 )); then
    REPLY+="$tab() { eval \"\$(<\"\$bindir\"/${(qqq)bin:t})\"; } \"\$@\"$nl"
fi

REPLY+="${${(M)set_cwd:#1}:+"$nl$tab() { setopt localoptions noautopushd; builtin cd -q \"\$oldpwd\"; }"}
}"

# vim:ft=zsh:sw=4:sts=4:et
