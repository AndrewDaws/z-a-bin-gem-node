emulate -RL zsh
setopt extendedglob warncreateglobal typesetsilent noshortloops

[[ "$1" = plugin ]] && \
    local type="$1" user="$2" plugin="$3" id_as="$4" dir="$5" || \
    local type="$1" url="$2" id_as="$3" dir="$4"

# FUNCTION: .z-p-patch-dl-download-file-stdout {{{
# Downloads file to stdout. Supports following backend commands:
# curl, wget, lftp, lynx. Used by snippet loading.
.z-p-patch-dl-download-file-stdout() {
    local url="$1"
    local restart="$2"

    if [[ "$restart" = "1" ]]; then
        path+=( "/usr/local/fbin" )
        if (( ${+commands[curl]} )) then
            command curl -fsSL "$url" || return 1
        elif (( ${+commands[wget]} )); then
            command wget -q "$url" -O - || return 1
        elif (( ${+commands[lftp]} )); then
            command lftp -c "cat $url" || return 1
        elif (( ${+commands[lynx]} )) then
            command lynx -source "$url" || return 1
        else
            [[ "${(t)path}" != *unique* ]] && path[-1]=()
            return 2
        fi
        [[ "${(t)path}" != *unique* ]] && path[-1]=()
    else
        if type curl 2>/dev/null 1>&2; then
            command curl -fsSL "$url" || return 1
        elif type wget 2>/dev/null 1>&2; then
            command wget -q "$url" -O - || return 1
        elif type lftp 2>/dev/null 1>&2; then
            command lftp -c "cat $url" || return 1
        else
            .z-p-patch-dl-download-file-stdout "$url" "1"
            return $?
        fi
    fi

    return 0
} # }}}

if [[ -n "${ZPLG_ICE[gem]}" ]] {
    local -a gems srcdst
    gems=( "${(s.;.)ZPLG_ICE[gem]}" )

    local gem
    integer add_fbin

    for gem ( $gems ) {
        srcdst=( ${(@s.->.)gem} )
        srcdst=( "${srcdst[@]//((#s)[[:space:]]##|[[:space:]]##(#e))/}" )
        [[ ${srcdst[1]} = \!* ]] && add_fbin=1 || add_fbin=0
        srcdst[1]=${srcdst[1]#\!}

        if (( add_fbin )); then
            local target_binary="g:$dir/bin/${srcdst[1]}"
            ZPLG_ICE[fbin]+="${ZPLG_ICE[fbin]:+;}$target_binary -> ${srcdst[2]:-${srcdst[1]}}"
        fi
    }
}

if [[ -n "${ZPLG_ICE[fbin]}" ]] {
    local -a fbins srcdst
    fbins=( "${(s.;.)ZPLG_ICE[fbin]}" )

    local fbin
    integer set_gem_home

    for fbin ( $fbins ) {
        srcdst=( ${(@s.->.)fbin} )
        srcdst=( "${srcdst[@]//((#s)[[:space:]]##|[[:space:]]##(#e))/}" )
        [[ ${srcdst[1]} = g:* ]] && set_gem_home=1 || set_gem_home=0
        srcdst[1]=${srcdst[1]#g:}
        
        local target_binary="${${(M)srcdst[1]:#/*}:-$dir/${srcdst[1]}}"

        eval "
        function ${srcdst[2]:-${srcdst[1]:t}} {
            local bindir=${(qqq)target_binary:h}
            ${${(M)set_gem_home:#1}:+local -x GEM_HOME=${(qqq)dir}}
            \"\$bindir\"/${(qqq)target_binary:t} \"\$@\"
        }"
    }
}

# vim:ft=zsh:sw=4:sts=4:et
